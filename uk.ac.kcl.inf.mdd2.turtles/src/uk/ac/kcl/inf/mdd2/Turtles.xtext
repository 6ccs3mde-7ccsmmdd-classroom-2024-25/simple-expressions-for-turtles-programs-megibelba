grammar uk.ac.kcl.inf.mdd1.Turtles with org.eclipse.xtext.common.Terminals

generate turtles "http://www.ac.uk/kcl/inf/mdd1/Turtles"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

TurtlesProgram:
    statements += Statement+
;

Statement:
    VariableDeclaration |
    MoveStatement |
    TurnStatement |
    LoopStatement
;

VariableDeclaration:
    'var' name=ID '=' value=INT
;

LoopStatement:
    count = Addition 'times' 'do' 
    	statements+=Statement+ 
    'end'
;

MoveStatement:
    command = MoveCommand '(' steps = Addition ')'
;

enum MoveCommand:
    forward | backward
;

TurnStatement:
    'turn' direction = TurnCommand 'by' degrees = REAL 'degrees'
;

enum TurnCommand:
    left | right
;

Addition returns IntExpression:
	Multiplication ({Addition.left=current} operator+=('+'|'-') right += Multiplication)*
;

Multiplication returns IntExpression:
	PrimaryExpression ({Multiplication.left=current} operator+=('*'|'/') right += PrimaryExpression)*
;

PrimaryExpression returns IntExpression:
    IntLiteral | 
    VariableReference |
    '(' Addition ')'
;

IntLiteral:
	val = INT
;

VariableReference:
	var=[VariableDeclaration]
;

REAL returns ecore::EFloat hidden():
	INT '.' INT
;
